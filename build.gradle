plugins {
    id "base"
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
}

apply plugin: 'kotlin'
apply plugin: 'groovy'

repositories {
   mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}

sourceSets {
    integrationTest {
        groovy.srcDir "$projectDir/src/integration-test/groovy"
        resources.srcDir "$projectDir/src/integration-test/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest

ext {
    jGivenVersion = '0.18.2'
    ratpackVersion = '1.8.0'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "io.ratpack:ratpack-core:$ratpackVersion"
    implementation "org.slf4j:slf4j-api:1.7.28"
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "io.arrow-kt:arrow-core:0.10.5"

    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
    testImplementation "io.ratpack:ratpack-test:$ratpackVersion"
    testImplementation "org.codehaus.groovy:groovy-all:2.5.4"
    testRuntime "org.objenesis:objenesis:3.1"
    testRuntime "net.bytebuddy:byte-buddy:1.10.10"

    integrationTestImplementation "com.tngtech.jgiven:jgiven-core:$jGivenVersion"
    integrationTestImplementation "com.tngtech.jgiven:jgiven-html5-report:$jGivenVersion"
    integrationTestImplementation "com.tngtech.jgiven:jgiven-spock:$jGivenVersion"
    integrationTestImplementation "io.ratpack:ratpack-groovy-test:$ratpackVersion"
}

compileGroovy {
    dependsOn tasks.getByPath('compileKotlin')
    classpath += files(compileKotlin.destinationDir)
}

compileTestGroovy {
    dependsOn tasks.getByPath('compileTestKotlin')
    classpath += files(compileTestKotlin.destinationDir)
}